apply plugin: 'java'
apply plugin: 'idea'
apply plugin: "findbugs"
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.johnrengelman.shadow'


group = 'org.komamitsu'
archivesBaseName = 'fluency'
version = '1.8.0-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

def deployUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ''
def deployPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ''

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'ch.qos.logback:logback-classic:1.1.8'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile 'org.msgpack:jackson-dataformat-msgpack:0.8.14'
    compile 'org.komamitsu:phi-accural-failure-detector:0.0.4'
}

findbugs {
    effort = 'max'
    excludeFilter = file("${rootProject.projectDir}/findbugs-exclude.xml")
}

shadowJar {
    relocate 'com.fasterxml.jackson', 'org.komamitsu.thirdparty.jackson'
    classifier = 'shadow'
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, shadowJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: deployUsername, password: deployPassword)
            }

            pom.project {
                name 'fluency'
                description "Yet another fluent logger"
                packaging 'jar'
                url 'https://github.com/komamitsu/fluency'
                scm {
                    connection 'scm:git:git://github.com/komamitsu/fluency.git'
                    developerConnection 'scm:git:git@github.com:komamitsu/fluency.git'
                    url 'https://github.com/komamitsu/fluency'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'komamitsu'
                        name 'Mitsunori Komatsu'
                        email 'komamitsu@gmail.com'
                    }
                }
            }
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
